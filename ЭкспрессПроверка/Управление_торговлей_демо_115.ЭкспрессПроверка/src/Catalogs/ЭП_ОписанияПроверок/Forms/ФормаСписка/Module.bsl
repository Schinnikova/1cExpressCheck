#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПроверитьОтчет(Команда)

	ТекущаяСтрока = Элементы.Список.ТекущиеДанные;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;

	СтруктураАдреса = ПолучитьСтруктуруАдресаНаСервере(ТекущаяСтрока.Ссылка);
	// добавляем параметры в пользовательские настройки
	НастройкиОтчета = ПолучитьИзВременногоХранилища(СтруктураАдреса.НастройкиКомпоновкиДанных);
	Для Каждого ПараметрОтчета Из НастройкиОтчета.ПараметрыДанных.Элементы Цикл
		Если ПараметрОтчета.Использование И ПараметрОтчета.ИдентификаторПользовательскойНастройки = "" Тогда
			ПараметрОтчета.ИдентификаторПользовательскойНастройки = Новый УникальныйИдентификатор;
		КонецЕсли;
	КонецЦикла;

	Парам = Новый Структура;
	Парам.Вставить("АдресСхемы", СтруктураАдреса.СхемаКомпоновкиДанных);
	Парам.Вставить("Вариант", НастройкиОтчета);
	Парам.Вставить("КлючВарианта", Строка(ТекущаяСтрока.Ссылка.УникальныйИдентификатор())); 

	ОткрытьФорму("Отчет.ЭП_ПроизвольнаяРасшифровка.Форма", Парам, ЭтотОбъект, УникальныйИдентификатор);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьСтруктуруАдресаНаСервере(ОписаниеСсылка)

	Возврат Справочники.ЭП_ОписанияПроверок.АдресаСхемыКомпоновкиДанныхИНастроекВоВременномХранилище(ОписаниеСсылка);

КонецФункции

#КонецОбласти