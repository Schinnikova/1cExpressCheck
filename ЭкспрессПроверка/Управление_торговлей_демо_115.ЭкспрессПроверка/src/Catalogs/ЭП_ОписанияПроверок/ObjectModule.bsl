#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	// проверка на дубли наименований
	Если НЕ ЭтоГруппа И НЕ ПометкаУдаления Тогда
		НаименованиеУникально = Справочники.ЭП_ОписанияПроверок.НаименованиеУникально(ЭтотОбъект);
		Если НЕ НаименованиеУникально Тогда
			ВызватьИсключение НСтр("ru='Наименование не уникально'");
		КонецЕсли;
	КонецЕсли;

	ДополнительныеСвойства.Вставить("ЗаписьНовогоОбъекта", ЭтоНовый());

КонецПроцедуры

Процедура ПриЗаписи(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;

	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;

	СоздатьОбновитьВариантыОтчета();

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если Не ЭтоГруппа Тогда
		ПроверяемыеРеквизиты.Добавить("Родитель");
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СоздатьОбновитьВариантыОтчета()

	УстановитьПривилегированныйРежим(Истина);
	ОтчетРодитель = Справочники.ИдентификаторыОбъектовРасширений.НайтиПоРеквизиту("ПолноеИмя", "Отчет.ПроизвольнаяРасшифровка");
	Если ДополнительныеСвойства.ЗаписьНовогоОбъекта Тогда
		НовыйВариантОтчета = Справочники.ВариантыОтчетов.СоздатьЭлемент();
		НовыйВариантОтчета.Наименование = Наименование;
		НовыйВариантОтчета.ВидимостьПоУмолчанию = Ложь;
		НовыйВариантОтчета.Отчет = ОтчетРодитель;
		НовыйВариантОтчета.Пользовательский = Истина;
		НовыйВариантОтчета.ТипОтчета = Перечисления.ТипыОтчетов.Расширение;
		НовыйВариантОтчета.Настройки = ХранилищеНастроекКомпоновкиДанных;
		НовыйВариантОтчета.КлючВарианта = Строка(Ссылка.УникальныйИдентификатор());
		НовыйВариантОтчета.Записать();
	Иначе
		// обновим наименование и настройки у варианта отчета
		ВариантОтчета = ВариантОтчета(ОтчетРодитель, Строка(Ссылка.УникальныйИдентификатор()));
		Если ВариантОтчета = Неопределено Тогда
			Возврат;
		КонецЕсли;
		ВариантОтчетаОбъект = ВариантОтчета.ПолучитьОбъект();
		ВариантОтчетаОбъект.Наименование = Наименование;
		ВариантОтчетаОбъект.Настройки = ХранилищеНастроекКомпоновкиДанных;
		ВариантОтчетаОбъект.Записать();
	КонецЕсли;

КонецПроцедуры

Функция ВариантОтчета(Отчет, КлючВарианта) Экспорт
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	ВариантыОтчетов.Ссылка КАК ВариантОтчета
		|ИЗ
		|	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
		|ГДЕ
		|	ВариантыОтчетов.Отчет = &Отчет
		|	И ВариантыОтчетов.КлючВарианта = &КлючВарианта
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВариантыОтчетов.ПометкаУдаления";
		
	Запрос.УстановитьПараметр("Отчет", Отчет);
	Запрос.УстановитьПараметр("КлючВарианта", КлючВарианта);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.ВариантОтчета;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

#КонецОбласти

#КонецЕсли